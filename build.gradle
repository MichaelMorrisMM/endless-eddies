/*
 * This is the endless eddies build config
 */
plugins {
    id 'com.github.gmazzo.sqlite' version '0.2'
    id 'com.moowork.node' version '1.2.0'
    id 'war'
}

def confDir = System.getenv('ENDLESS_EDDIES_CONFIG_DIR')
def serverDir = System.getenv('CATALINA_HOME')
def deployPath = serverDir + '/webapps/' + rootProject.name
def javaDestPath = deployPath + '/WEB-INF/classes'
def serverLibPath = deployPath + '/WEB-INF/lib'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.auth0:java-jwt:3.4.0'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.glassfish:javax.json:1.1.2'
    implementation 'org.xerial:sqlite-jdbc:3.23.1'
}

task cleanAll(type: GradleBuild) {
    tasks = ['cleanProject', 'cleanDeployment', 'cleanWar']
}

task cleanProject(type: GradleBuild) {
    tasks = ['clean', 'cleanAngular']
}

task cleanDeployment(type: Delete) {
    delete new File(deployPath)
}

task cleanWar(type: Delete) {
    delete new File(deployPath + '.war')
}

task cleanAngular(type: Delete) {
    delete 'dist'
}

task cleanJavaDeploy(type: Delete) {
    delete new File(javaDestPath)
}

task seedLibs {
    def libDest = new File(serverLibPath)
    if (!libDest.exists()) {
        libDest.mkdirs()
    }
}

task deployLibs(type: Copy) {
    from configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }
    into serverLibPath
    eachFile {
        if (it.relativePath.getFile(destinationDir).exists()) {
            it.exclude()
        }
    }
    dependsOn seedLibs
}

task seedDeployment {
    def deploy = new File(deployPath)
    if (!deploy.exists()) {
        deploy.mkdirs()
    }
}

task seedJava {
    def javaDest = new File(javaDestPath)
    if (!javaDest.exists()) {
        javaDest.mkdirs()
    }
}

task deployJava(type: Copy) {
    from sourceSets.main.output.classesDirs
    into javaDestPath
    dependsOn deployLibs, cleanJavaDeploy, seedJava, classes
}

npm_run_build.dependsOn npmInstall

task deployAngular(type: Copy) {
    from 'dist'
    into deployPath
    dependsOn seedDeployment, npm_run_build
}

war {
    from 'dist'
    dependsOn npm_run_build
}

task deployWar(type: Copy) {
    from "$buildDir/libs"
    into serverDir + '/webapps/'
    include '*.war'
    dependsOn war, cleanDeployment
}

sqlite {
    driverDependency 'org.xerial:sqlite-jdbc:3.23.1'
}

task makeDatabase {
    def databaseFile = file("$confDir/endless_eddies.db")

    outputs.file databaseFile

    doLast {
        def db = openSQLiteDatabase(databaseFile)
        db.execute 'DROP TABLE IF EXISTS user'
        db.execute '''CREATE TABLE user(
                        idUser      INTEGER         PRIMARY KEY,
                        email       VARCHAR(320)    NOT NULL UNIQUE,
                        password    VARCHAR(100)    NOT NULL,
                        salt        VARCHAR(150)    NOT NULL,
                        isAdmin     CHAR(1)         NOT NULL
                   )'''
        db.execute 'DROP TABLE IF EXISTS request'
        db.execute '''CREATE TABLE request(
                        idRequest   INTEGER         PRIMARY KEY,
                        name        VARCHAR(100)    NOT NULL UNIQUE,
                        idUser      INTEGER,
                        idGuest     INTEGER,
                        date        VARCHAR(30)     NOT NULL,
                        expiration  INTEGER,
                        FOREIGN KEY(idUser) REFERENCES user(idUser)
                   )'''
    }
}
